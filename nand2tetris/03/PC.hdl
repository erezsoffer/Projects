// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Use Inc chip to set the incremented value
    Inc16(in=val, out=incedVal);
    // Use Mux chips to select between inc, load and reset
    Mux16(a=val, b=incedVal, sel=inc, out=incOut);
    Mux16(a=incOut, b=in, sel=load, out=loadOut);
    Mux16(a=loadOut, b=false, sel=reset, out=resetOut);
    // Use register to set the output
    Register(in=resetOut, load=true, out=val, out=out);
}
